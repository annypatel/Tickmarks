buildscript {
    def appVersion = [
            'major': 1,
            'minor': 0,
            'patch': 0,
            'build': 0,
    ]

    // Build configuration
    ext.buildConfig = [
            'minSdk'     : 15,
            'compileSdk' : 28,
            'targetSdk'  : 28,
            'versionName': "${appVersion.major}.${appVersion.minor}.${appVersion.patch}",
            'versionCode': appVersion.major * 1000000 + appVersion.minor * 10000 + appVersion.patch * 100 + appVersion.build

    ]

    // Dependency versions
    ext.versions = [
            'agp'     : '3.2.0-alpha18',
            'kotlin'  : '1.2.41',
            'rxjava'  : '2.1.14',
            'spotless': '3.13.0',
            'ktlint'  : '0.23.1'
    ]

    // Dependencies
    ext.deps = [
            'kotlin': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin",
            'rxjava': "io.reactivex.rxjava2:rxjava:$versions.rxjava"
    ]

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$versions.agp"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:$versions.spotless"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint(versions.ktlint)
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}